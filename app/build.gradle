buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {

    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.ofg.uptodate'
//apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'realm-android'
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/keystore.gradle"

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"
apply from: "$rootDir/gradle/pmd.gradle"

check.dependsOn('checkstyle')
check.dependsOn('findbugs')
check.dependsOn('pmd')
check.dependsOn('lint')

repositories {
    maven { url 'https://clojars.org/repo/' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {

        applicationId = versions.packagename

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode buildVersionCode()
        versionName buildVersionName()

        testApplicationId "${versions.packagename}.tests"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

//        jackOptions {
//            enabled false
//        }

        // Enabling vector drawables support.
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/$keystore.debugKeystore")
            storePassword "$keystore.debugStorePass"
            keyAlias "$keystore.debugAliase"
            keyPassword "$keystore.debugKeyPass"
        }
        release {
            storeFile file("../keystore/$keystore.releaseKeystore")
            storePassword "$keystore.releaseStorePass"
            keyAlias "$keystore.releaseAliase"
            keyPassword "$keystore.releaseKeyPass"
        }
    }

    buildTypes {
        debug {
            resValue "string", "app_name", "(D)${versions.appname}"
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
            signingConfig signingConfigs.debug
            testCoverageEnabled = false
        }
        release {
            resValue "string", "app_name", "${versions.appname}"
            minifyEnabled false
            shrinkResources false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.pro')
            signingConfig signingConfigs.release
        }
    }

    ////////////////////////////////////////////////////////
    // Separate resources by corresponding screen folders //
    ////////////////////////////////////////////////////////
    sourceSets {

        // Unit tests live in src/unitTest.
        test.java.srcDir 'src/mockTest/java'

        // Resources
        main {
            res.srcDirs = [
                    'src/main/res'
            ]
            file('src/main/res-screen')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'InvalidPackage'
    }

}

dependencies {
    implementation project(':liquidview')

    // Support libraries
    implementation deps.support.app_compat
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.recyclerview
    implementation deps.support.support_annotations
    implementation deps.support.cardview

    // DI
    implementation deps.dagger.runtime
    annotationProcessor deps.dagger.compiler



    compileOnly "javax.annotation:jsr250-api:1.0"

    // Ui
    implementation "com.jakewharton:butterknife:8.8.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
    implementation 'com.github.princeparadoxes:recyclerbindableadapter:1.3'
    implementation 'com.livetyping:cannyviewanimator:0.8'
    implementation 'com.yarolegovich:discrete-scrollview:1.1.4'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'com.diogobernardino:williamchart:2.5.0'
    implementation "com.daimajia.swipelayout:library:1.2.0@aar"

    implementation "com.jakewharton.timber:timber:4.6.0"
    //rx
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxandroid}"

    // Debug
    debugImplementation "com.facebook.stetho:stetho:${versions.stetho}"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:${versions.stetho}"
    debugImplementation 'io.palaima.debugdrawer:debugdrawer:0.7.0'
    releaseImplementation 'io.palaima.debugdrawer:debugdrawer-no-op:0.7.0'
    debugImplementation 'io.palaima.debugdrawer:debugdrawer-commons:0.7.0'
    debugImplementation 'io.palaima.debugdrawer:debugdrawer-actions:0.7.0'
    debugImplementation 'io.palaima.debugdrawer:debugdrawer-okhttp:0.7.0'
    debugImplementation 'io.palaima.debugdrawer:debugdrawer-okhttp3:0.7.0'
    debugImplementation 'io.palaima.debugdrawer:debugdrawer-glide:0.7.0'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}"
    debugImplementation "com.squareup.retrofit2:retrofit-mock:${versions.retrofit}"

    // Unit Tests
    androidTestImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-library:1.4-atlassian-1"
    testImplementation "org.mockito:mockito-core:2.11.0"
    testImplementation "com.google.truth:truth:0.28"


    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}

//dexcount {
//    format = "list"
//    includeClasses = false
//    includeFieldCount = true
//    includeTotalMethodCount = false
//    orderByMethodCount = false
//    verbose = false
//    maxTreeDepth = Integer.MAX_VALUE
//    teamCityIntegration = false
//    teamCitySlug = null
//    runOnEachAssemble = true
//}

///////////////////////////////////////////////////
///////////////// Change apk name /////////////////
///////////////////////////////////////////////////
android.applicationVariants.all { variant ->
//    for (output in variant.outputs) {
//        def outputFile = output.outputFile
//        if (outputFile != null && outputFile.name.endsWith('.apk')) {
//            def fileName = "${versions.apkname}-${output.name}.apk"
//            output.outputFile = new File(outputFile.parent, fileName)
//        }
//    }
}

///////////////////////////////////////////////////
///////////// Print build finish time /////////////
///////////////////////////////////////////////////
gradle.buildFinished { buildResult ->
    def buildFinishDate = new Date()
    def formattedDate = buildFinishDate.format('yyyy-MM-dd HH:mm:ss')
    println "Build finished: ${formattedDate}"
}
